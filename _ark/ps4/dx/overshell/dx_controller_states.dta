(kState_RB4DXControllerSettings
   (view {$this get_auto_list_view }) 
   (enter
      {$this setup_confirm_actions a 
      (
         "Change instrument type"
         "Change pad bindings"
      )
   }
   ) 
   (SELECT_MSG 
      {local $list_data 
         {
            ($objId UIListCom)get_data_in_column token }}  
      {switch $list_data 
         ("Change instrument type"
            {$this show_state kState_RB4DXSlotOverride}
         )
         ("Change pad bindings"
            {$this show_state kState_RB4DXBindingOverride}
         )
      }
   ) 
   (on_cancel 
      {$this show_state kState_RB4DXMain}
      {$this trigger_cancel_effects }
   )
)
#define RB4DXSLOT_DATA
(
   {$this setup_confirm_actions "Select the controller type you want to change:"
      (
         {switch $gtrslt
            (gtr "guitar: guitar")
            (drum "guitar: drums")
            (vox "guitar: vocals")
         }
         {switch $drmslt
            (gtr "drums: guitar")
            (drum "drums: drums")
            (vox "drums: vocals")
         }
         {switch $voxslt
            (gtr "vocals: guitar")
            (drum "vocals: drums")
            (vox "vocals: vocals")
         }
      )
   }
)
(kState_RB4DXSlotOverride
   (view confirm_action) 
   (enter
      RB4DXSLOT_DATA
      {set $slotentered TRUE}
   ) 
   (SELECT_MSG 
      {local $list_data 
         {
            ($objId UIListCom)get_data_in_column token }}  
      {switch $list_data 
         (("guitar: guitar" "guitar: drums" "guitar: vocals")
            {set $slot_changed gtr}
            {$this show_state kState_RB4DXSlotChooser}
            {$this trigger_select_effects }
         )
         (("drums: guitar" "drums: drums" "drums: vocals")
            {set $slot_changed drum}
            {$this show_state kState_RB4DXSlotChooser}
            {$this trigger_select_effects }
         )
         (("vocals: guitar" "vocals: drums" "vocals: vocals")
            {set $slot_changed vox}
            {$this show_state kState_RB4DXSlotChooser}
            {$this trigger_select_effects }
         )
      }
   ) 
   (on_cancel 
      {$this show_state kState_RB4DXMain}
      {$this trigger_cancel_effects }
      {set $slotentered FALSE}
   )
)
(kState_RB4DXSlotChooser
   (view confirm_action) 
   (enter
      {$this setup_confirm_actions "Select the controller type you want to change to:"
      (
         "guitar"
         "drums"
         "vocals"
      )
   }
   ) 
   (SELECT_MSG 
      {local $list_data 
         {
            ($objId UIListCom)get_data_in_column token }}  
      {switch $list_data 
         ("guitar"
            {set $slot_to gtr}
         )
         ("drums"
            {set $slot_to drum}
         )
         ("vocals"
            {set $slot_to vox}
         )
      }
      CLEAR_SLOT_DTAS
      WRITE_SLOT_DTA
      SET_SLOT_VAR
      CLEAR_INVALID_SLOT_DTAS
      {$overshell update_all}
      {$this trigger_select_effects }
   )
   (exit
      {set $slot_changed FALSE}
      {set $slot_to FALSE}
   )
   (on_cancel 
      {set $slot_changed FALSE}
      {$this show_state kState_RB4DXSlotOverride}
      {$this trigger_cancel_effects }
   )
)
(kState_RB4DXBindingOverride
   (view {$this get_auto_list_view }) 
   (enter
      {$this setup_confirm_actions a 
      (
         "[Reset]"
         "Rock Band 1-3 Guitar"
         "Rock Band 1-3 Drums"
         "Rock Band 4 Guitar"
         "Rock Band 4 Drums"
         "Guitar Hero Guitar"
         "Guitar Hero Drums"
      )
   }
   ) 
   (SELECT_MSG 
      {local $list_data 
         {
            ($objId UIListCom)get_data_in_column token }}
      DELETE_PAD_BINDING_DTAS
      {switch $list_data 
         ("[Reset]")
         ("Rock Band 1-3 Guitar"
            {write_file "data:/GoldHEN/RB4DX/settings/controller/binding/pad/rbgtr.dta" $dx_write_nul}
         )
         ("Rock Band 1-3 Drums"
            {write_file "data:/GoldHEN/RB4DX/settings/controller/binding/pad/rbdrum.dta" $dx_write_nul}
         )
         ("Rock Band 4 Guitar"
            {write_file "data:/GoldHEN/RB4DX/settings/controller/binding/pad/rb4gtr.dta" $dx_write_nul}
         )
         ("Rock Band 4 Drums"
            {write_file "data:/GoldHEN/RB4DX/settings/controller/binding/pad/rb4drum.dta" $dx_write_nul}
         )
         ("Guitar Hero Guitar"
            {write_file "data:/GoldHEN/RB4DX/settings/controller/binding/pad/ghgtr.dta" $dx_write_nul}
         )
         ("Guitar Hero Drums"
            {write_file "data:/GoldHEN/RB4DX/settings/controller/binding/pad/ghdrum.dta" $dx_write_nul}
         )
      }
      SET_PAD_BINDING_VAR
      SET_PAD_BINDING_HOOK
      {$overshell update_all}
      {$this show_state kState_RB4DXControllerSettings}
      {$this trigger_select_effects }
   ) 
   (on_cancel 
      {$this show_state kState_RB4DXControllerSettings}
      {$this trigger_cancel_effects }
   )
)