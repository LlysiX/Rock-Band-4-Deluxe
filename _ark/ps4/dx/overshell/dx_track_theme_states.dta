;Track Theme
#define RB4DXTHM_DATA
(
   {$this setup_confirm_actions a 
      (
         "Highway Texture"
         "Gem Texture"
         "Strikeline Texture"
         {if_else $noshine "Highway Shine: OFF" "Highway Shine: ON"}
         {if_else $gemshine "Gem Shine: OFF" "Gem Shine: ON"}
      )
   }
)
(kState_RB4DXTrackTheme
   (view {$this get_auto_list_view}) 
   (enter
      {set $noshine {dx_file_exists "ps4/track/shared/track_skin_swap.sgraph_ps4ps4"}}
      {set $gemshine {dx_file_exists "ps4/track/guitar/gem_unlit.sgraph_ps4ps4"}}
      {set $dx_cur_state thm}
      RB4DXTHM_DATA
   ) 
   (SELECT_MSG 
      {local $list_data {($objId UIListCom) get_data_in_column token}}  
      {switch $list_data 
         (("Highway Shine: ON" "Highway Shine: OFF")
            {set $noshine {! $noshine}}
            {if_else $noshine
               {do
                  {dx_file_rename "ps4/track/shared/track_skin_swap1.sgraph_ps4ps4" "ps4/track/shared/track_skin_swap.sgraph_ps4ps4"}
                  {dx_write_null_file "settings/visuals/noshine.dta"}
               }
               {do
                  {dx_file_rename "ps4/track/shared/track_skin_swap.sgraph_ps4ps4" "ps4/track/shared/track_skin_swap1.sgraph_ps4ps4"}
                  {dx_file_delete "settings/visuals/noshine.dta"}
               }
            }
            {if_else {$this in_song}
               {$this show_state kState_RB4DXInGameWarn}
               RB4DXTHM_DATA
            }
            {$this trigger_select_effects}
         )
         (("Gem Shine: ON" "Gem Shine: OFF")
            {set $gemshine {! $gemshine}}
            {if_else $gemshine
               {do
                  {dx_file_rename "ps4/track/guitar/gem_unlit1.sgraph_ps4ps4" "ps4/track/guitar/gem_unlit.sgraph_ps4ps4"}
                  {dx_file_rename "ps4/track/drums/gem_cymbal_unlit1.sgraph_ps4ps4" "ps4/track/drums/gem_cymbal_unlit.sgraph_ps4ps4"}
                  {dx_write_null_file "settings/visuals/gemshine.dta"}
               }
               {do
                  {dx_file_rename "ps4/track/guitar/gem_unlit.sgraph_ps4ps4" "ps4/track/guitar/gem_unlit1.sgraph_ps4ps4"}
                  {dx_file_rename "ps4/track/drums/gem_cymbal_unlit.sgraph_ps4ps4" "ps4/track/drums/gem_cymbal_unlit1.sgraph_ps4ps4"}
                  {dx_file_delete "settings/visuals/gemshine.dta"}
               }
            }
            {if_else {$this in_song}
               {$this show_state kState_RB4DXInGameWarn}
               RB4DXTHM_DATA
            }
            {$this trigger_select_effects}
         )
         ("Highway Texture"
            {$this show_state kState_RB4DXHWTexSelector}
            {$this trigger_select_effects}
         )
         ("Gem Texture"
            {$this show_state kState_RB4DXGemTexSelector}
            {$this trigger_select_effects}
         )
         ("Strikeline Texture"
            {$this show_state kState_RB4DXSLTexSelector}
            {$this trigger_select_effects}
         )
      }
   ) 
   (on_cancel 
      {set $dx_cur_state FALSE}
      {$this show_state kState_RB4DXTrkSettings}
      {$this trigger_cancel_effects}
   )
)

;Highway Texture Selector
(kState_RB4DXHWTexSelector
   (view {$this get_auto_list_view})
   (enter
      {set $texturelist ()}
      {set $texturelist_ ()}
      {resize $texturelist 0}
      {push_back $texturelist "[Default]"}
      {set $texturelist_ {file_list "data:/GoldHEN/RB4DX/ps4/dx/custom_textures/highways/*.png_ps4"}}
      {remove_elem $texturelist_ {elem $texturelist_ 0}}
      {foreach $entry $texturelist_
         {push_back $texturelist $entry}
      }
      {$this setup_confirm_actions a $texturelist}
      ;{set $customhw {|| {dx_file_exists "settings/visuals/highwaytex.dta"} {dx_file_exists "ps4/track/shared/gem_track_texture/gem_track_surface.bmp_ps4"}}}
      ;SET_HIGHWAY_TEX_VAR
   )
   (SELECT_MSG
      {local $list_data {($objId UIListCom) get_data_in_column token}}
      {dx_file_delete "ps4/track/shared/track_pattern_center_util.bmp_ps4"}
      {dx_file_delete "ps4/track/shared/track_pattern_side_util.bmp_ps4"}
      {dx_file_delete "ps4/track/shared/gem_track_texture/gem_track_surface.bmp_ps4"}
      {unless {== $list_data "[Default]"}
         {if_else {== $list_data "yarg"}
            {do
               {dx_file_copy {sprint "ps4/dx/custom_textures/highways/" $list_data ".png_ps4"} "ps4/track/shared/track_pattern_center_util.bmp_ps4"}
               {dx_file_copy {sprint "ps4/dx/custom_textures/highways/" $list_data ".png_ps4"} "ps4/track/shared/track_pattern_side_util.bmp_ps4"}
               {dx_file_copy {sprint "ps4/dx/custom_textures/highways/special/" $list_data ".png_ps4"} "ps4/track/shared/gem_track_texture/gem_track_surface.bmp_ps4"}
            }
            {do
               {dx_file_copy "ps4/track/shared/track_pattern_center_util1.bmp_ps4" "ps4/track/shared/track_pattern_center_util.bmp_ps4"}
               {dx_file_copy "ps4/track/shared/track_pattern_side_util1.bmp_ps4" "ps4/track/shared/track_pattern_side_util.bmp_ps4"}
               {dx_file_copy {sprint "ps4/dx/custom_textures/highways/" $list_data ".png_ps4"} "ps4/track/shared/gem_track_texture/gem_track_surface.bmp_ps4"}
            }
         }
      }
      {if_else {$this in_song}
         {$this show_state kState_RB4DXInGameWarn}
         {$this show_state kState_RB4DXTrackTheme}
      }
      {$this trigger_select_effects}
   )
   (on_cancel 
      {$this show_state kState_RB4DXTrackTheme}
      {$this trigger_cancel_effects}
   )
)

;Gem Texture Selector
(kState_RB4DXGemTexSelector
   (view {$this get_auto_list_view})
   (enter
      {$this setup_confirm_actions a ("Rock Band 4" "Rock Band 4 (Beta)")}
   )
   (SELECT_MSG
      {local $list_data {($objId UIListCom) get_data_in_column token}}
      {switch $list_data
         ("Rock Band 4"
            {dx_file_delete "ps4/track/guitar/gem_unlit.entity_ps4"}
            {dx_file_delete "ps4/track/guitar/gem_hopo_unlit.entity_ps4"}
         )
         ("Rock Band 4 (Beta)"
            {dx_file_copy "ps4/track/guitar/gem.entity_ps4" "ps4/track/guitar/gem_unlit.entity_ps4"}
            {dx_file_copy "ps4/track/guitar/gem.entity_ps4" "ps4/track/guitar/gem_hopo_unlit.entity_ps4"}
         )
      }
      {if_else {$this in_song}
         {$this show_state kState_RB4DXInGameWarn}
         {$this show_state kState_RB4DXTrackTheme}
      }
      {$this trigger_select_effects}
   )
   (on_cancel 
      {$this show_state kState_RB4DXTrackTheme}
      {$this trigger_cancel_effects}
   )
)

;Smasher/Strikeline Texture Selector
(kState_RB4DXSLTexSelector
   (view {$this get_auto_list_view})
   (enter
      {$this setup_confirm_actions a ("Rock Band 4" "Rock Band 2")}
   )
   (SELECT_MSG
      {local $list_data {($objId UIListCom) get_data_in_column token}}
      {switch $list_data
         ("Rock Band 4"
            {dx_file_delete "ps4/track/smasher/smasher_normal.bmp_ps4"}
            {dx_file_delete "ps4/track/smasher/smasher_util.bmp_ps4"}
         )
         ("Rock Band 2"
            {dx_file_copy "ps4/track/smasher/smasher_normal_rb2.bmp_ps4" "ps4/track/smasher/smasher_normal.bmp_ps4"}
            {dx_file_copy "ps4/track/smasher/smasher_util_rb2.bmp_ps4" "ps4/track/smasher/smasher_util.bmp_ps4"}
         )
      }
      {if_else {$this in_song}
         {$this show_state kState_RB4DXInGameWarn}
         {$this show_state kState_RB4DXTrackTheme}
      }
      {$this trigger_select_effects}
   )
   (on_cancel 
      {$this show_state kState_RB4DXTrackTheme}
      {$this trigger_cancel_effects}
   )
)